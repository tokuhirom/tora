import os
import json
from glob import glob

conf = json.loads(open('../../config.json').read())

env = Environment(
    ENV={'PATH': conf['PATH']},
    CXXFLAGS=conf['CXXFLAGS'],
    CCFLAGS=conf['CCFLAGS'],
    LINKFLAGS=conf['LINKFLAGS'],
    CPPPATH=conf['CPPPATH'],
    CC=conf['CC'],
    CXX=conf['CXX'],
    SHLIBPREFIX=conf['SHLIBPREFIX'],
    LIBS=conf['LIBS'],
    tools=conf['TOOLS'],
)

if os.name == 'nt':
    extra = [
        '../../vendor/boost_1_49_0/libs/filesystem/v3/src/path_traits.cpp',
        '../../vendor/boost_1_49_0/libs/filesystem/v3/src/windows_file_codecvt.cpp',
        '../../vendor/boost_1_49_0/libs/filesystem/v3/src/codecvt_error_category.cpp',
    ]
else:
    extra = []


shlib = env.SharedLibrary("lib/File/Path" + env['SHLIBSUFFIX'], [
    'path.cc',
    '../../vendor/boost_1_49_0/libs/system/src/error_code.cpp',
    '../../vendor/boost_1_49_0/libs/filesystem/v3/src/operations.cpp',
    '../../vendor/boost_1_49_0/libs/filesystem/v3/src/path.cpp',
] + extra, LIBPATH=['../..'])
Default(shlib)

env.Command('test', shlib, conf['TORA_PROVE'] + " --exec '../../bin/tora -I lib -I ../../lib/' t/*.tra")

PREFIX=conf['PREFIX']

installs = []
for x in os.listdir('lib/'):
    dst = conf['TORA_LIBPREFIX'] + x
    src = "lib/" + x
    installs += [env.InstallAs(dst, src)]

env.Alias('install', [shlib, installs])

