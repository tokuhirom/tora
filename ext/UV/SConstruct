import os
import json
from glob import glob

conf = json.loads(open('../../config.json').read())

if os.name == 'nt':
    libs = conf['LIBS'] + ['uv', 'psapi', 'iphlpapi', 'pthread']
else:
    libs = conf['LIBS'] + ['m', 'rt', 'pthread']

env = Environment(
    ENV={'PATH': conf['PATH']},
    CXXFLAGS=conf['CXXFLAGS'],
    CCFLAGS=conf['CCFLAGS'],
    LINKFLAGS=conf['LINKFLAGS'],
    CPPPATH=conf['CPPPATH']+['../../vendor/libuv/include/'],
    CC=conf['CC'],
    CXX=conf['CXX'],
    SHLIBPREFIX=conf['SHLIBPREFIX'],
    LIBS=libs,
    tools=conf['TOOLS'],
)

if not os.path.exists("../../vendor/libuv/include/uv.h"):
    print "uv.h not found. You may forgot git submodule --init?"
    Exit(1)

# see http://stackoverflow.com/questions/2246399/scons-to-make-a-shared-library-so-with-a-static-libarary-a
env['STATIC_AND_SHARED_OBJECTS_ARE_THE_SAME']=1

libuv_make = 'make'
if os.name != 'nt':
    libuv_make += ' CFLAGS+=-fPIC'
libuv = env.Command('../../vendor/libuv/uv' + env.get('LIBSUFFIX'), None, libuv_make, chdir=1)

env.Append(LIBPATH=['../../vendor/libuv/', '../../'])

shlib = env.SharedLibrary("lib/UV_" + conf['TORA_EXTSUFFIX'], ['uv.cc', libuv])
Default(shlib)

env.Command('test', [shlib], 'prove -v --exec "../../bin/tora -I ../Socket/lib/ -I lib -I ../../lib/" t/*.tra')

installs = []
for x in os.listdir('lib/'):
    dst = conf['TORA_LIBPREFIX'] + x
    src = "lib/" + x
    installs += [env.InstallAs(dst, src)]

env.Alias('install', [shlib, installs])
