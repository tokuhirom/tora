import os
import json
from glob import glob

conf = json.loads(open('../../config.json').read())

env = Environment(
    CXXFLAGS=conf['CXXFLAGS'],
    CCFLAGS=conf['CCFLAGS'],
    LINKFLAGS=conf['LINKFLAGS'],
    CC=conf['CC'],
    CXX=conf['CXX'],
    SHLIBPREFIX=conf['SHLIBPREFIX'],
    tools=conf['TOOLS'],
)

libs = ['tora', 're2']
if os.name == 'nt':
    libs += ['ws2_32', 'pthread']

shlib = env.SharedLibrary("lib/Digest/MD5" + conf['TORA_EXTSUFFIX'], ['Digest-MD5.cc', 'md5.c'], SHLIBPREFIX='', LIBPATH=['../..'], LIBS=libs)
Default(shlib)

env.Command('test', shlib, '../../bin/tora -I lib -I ../../lib/ t/test.tra')

installs = []
for x in os.listdir('lib/'):
    dst = conf['TORA_LIBPREFIX'] + x
    src = "lib/" + x
    installs += [env.InstallAs(dst, src)]

env.Alias('install', [shlib, installs])
